

ORG 100H
LOAD 0B400H


PCNT:EQU 33H
PDATA:EQU 32H
INTVECT:EQU 0FB12H
BUSY:EQU 0FB92H


VDATA:EQU 8
VADD:EQU 9

SCREEN:EQU 3C00H
COMAND:EQU 1800H ;1C00H

MARKER:EQU 23H


KEY 3'|J|^M|MCB400 DFFF'
KEY 4' 100|MG100|MO'
KEY 5' '
KEY 6'|J|^SED1.COM B40'
KEY 7'0 DFFF|M'



ENTRY:JP EDIT

ST:EQU 4600H

;VARIABLE AREA


SOF:DW ST  ;START OF FILE PNTR
EOF:DW ST  ;END OF FILE PNTR
LINEST:DW ST  ;CURRENT LINE PNTR
CURRENT:DW ST
HIMEM:DW 0E100H
LBLP:DW AEND ;END OF LABLE TABLE

EOLBUF:DB 13,10,32
;EOLBUF CONTAINS WHAT TO SEND TO
;PRINTER AFTER PRINTING A LINE
;TERMINATED WITH CHR 32 (SPACE)

FRMFEED:DB 60

COLD:DB 255
;COLD MUST BE 0FFH IF COLD START
;REQUIRED


FEP:DW 0
TEMP:DW 0
PC:DW 0
PCSTART:DW 0
OBJ:DW 0
BMARK:DB 0
FINDB:DS 15
STRLG:DB 0
BUFF:DS 41
BUFF2:DS 40

BLDBUFF:DS 128

INCLBUF:DS 256

ERSP:DW 0
LFIND:DW ST
FBMARK:DW 0
ALL:DB 0
PNTADS:DS 16,0
BUILDAD:DW 0
BLDFLAG:DB 0
DELFLAG:DB 0
ASSLNG:DB 0
COMVCOL:DW 0
VIEWBUFF:DS 16

TBUFF:DS 128

TBUFFEND:


GETZKEY:
LD A,(BLDFLAG)
OR A
JR Z,NOTBUILD
LD HL,(BUILDAD)
LD A,(HL)
INC HL
LD (BUILDAD),HL
CP 1AH
RET NZ
XOR A
LD (BLDFLAG),A
NOTBUILD:DW ZKEYIN
RET

EDIT:
LD SP,TBUFFEND
EXX
LD DE,0
EXX
CALL SET40C
CALL CLS
LD A,(COLD)
OR A
JR Z,NOTCOLD
XOR A
LD (COLD),A
CALL COPYS]C
LD A,1
LD (COMVCOL),A
CALL SPRINT
DB 'MIKES ASSEMBLER'
DB 13,10
DB 'V28.0 (C) 1985 ME'
DB 13,10
DB 13,10
DB 'NEW OR OLD',10,13.S
NEWOLD?:CALL GETZKEY
CP 'N'
JR Z,NEW
CP 'O'
JR NZ,NEWOLD?
LD BC,0EBFFH-ST
LD HL,(SOF)
OLD1:INC HL
LD A,(HL)
CP 1AH
JR Z,GOTOLD
DEC BC
LD A,B
OR C
JR NZ,OLD1
NEW:LD HL,(SOF)
GOTOLD:LD (EOF),HL
NOTCOLD:CALL CKCOLD
CALL GETCUR
CALL SETKEYS
CALL HOME
CALL DRAWSCR
JP MAIN

COPYS]C:PUSH HL
PUSH DE
LD HL,(VCOL)
LD (COMVCOL),HL
LD HL,SCREEN
LD DE,COMAND
CALL COPYS
POP DE
POP HL
RET

COPYC]S:PUSH HL
PUSH DE
LD HL,(COMVCOL)
LD (VCOL),HL
LD DE,SCREEN
LD HL,COMAND
CALL COPYS
POP DE
POP HL
RET


;COPYS COPIES 240 BYTES
;FROM HL TO DE (IN VRAM)

COPYS:LD BC,VDATA
COPYLOOP:CALL TOHL
LD B,240
PUSH HL
LD HL,INCLBUF
INIR 
LD HL,INCLBUF
EX DE,HL
DEC HL
CALL TOHL
INC HL
EX DE,HL
LD B,240
OTIR 
POP HL
INC H
INC D
RET

GETNAM:
LD DE,FCB
PUSH DE
XOR A 
LD (DE),A
INC DE
LD B,11
LD A,32
GM1:LD (DE),A
INC DE
DJNZ GM1
LD B,24
XOR A
GM3:LD (DE),A
INC DE
DJNZ GM3
POP DE
LD HL,(NAMEAD)
INC HL
LD A,(HL)
DEC HL
CP ":"
JR NZ,GM2
LD A,(HL)
SUB "0"
JP C,NAMERR
CP 4
JP NC,NAMERR
INC A 
LD (DE),A
INC HL
INC HL

GM2:INC DE
CALL FNSP
LD B,8
GM4:LD A,(HL)
CP 13
JR Z,GOTN
CP "."
JR Z,GOTN
LD (DE),A
INC HL
INC DE
DJNZ GM4
JR GOTN2
GOTN:LD A,B
CP 8  
JR Z,NAMERR
GOTN2:CALL SETDMA
LD A,(HL)
CP "."
INC HL
PUSH HL
JR Z,F3L1
LD HL,MSRC
F3L1:LD DE,FCB+9
LDI
LDI
LD A,(HL)
AND 7FH
LD (DE),A
POP HL
LD DE,FCB+9
CALL CKFTYPE
CP SRC
JR Z,ISSRC
CP BAK
JR Z,ISSRC
INC HL
INC HL
INC HL
CALL FNSP

EX DE,HL
DW GETHL
PUSH HL
INC DE
CALL FNSP
DW GETHL
POP DE
LD A,D
OR E
JP Z,SERR
RET


ISSRC:
LD HL,(EOF)
LD (HL),1AH
INC HL
LD DE,(SOF)
RET

NAMERR:
CALL CLS
LD A,4
CALL PRMES
LD A,7
DW OUTC
LD SP,(ERSP)
CALL GETZKEY
JP CMNDAGAIN

SETDMA:
LD C,1AH
PUSH DE
PUSH HL
LD DE,DMA
CALL 5
POP HL
POP DE
RET

SETBAKUP:
LD DE,FCB+9
CALL CKFTYPE
CP 3
RET NC
ADD 3
JR SETYPE

SRC:EQU 0
COM:EQU 1
DAT:EQU 2
BAK:EQU 3
COP:EQU 4
DBU:EQU 5
BLD:EQU 6


SETSRC:LD A,SRC
JR SETYPE

SETCOM:LD A,COM
JR SETYPE

SETDAT:LD A,DAT
JR SETYPE

SETBAK:LD A,BAK
JR SETYPE

SETCOP:LD A,COP
JR SETYPE

SETDBU:LD A,DBU
JR SETYPE

SETBLD:LD A,BLD

SETYPE:PUSH HL
PUSH DE
PUSH BC
LD B,A
ADD A,A
ADD A,B
LD HL,MSRC
ADD L
LD L,A
JR NC,$+3
LD H,A
LD DE,FCB+9
LDI
LDI
LD A,(HL)
AND 7FH
LD (DE),A
POP BC
POP DE
POP HL
RET

CKFTYPE:PUSH HL
PUSH BC
LD HL,MSRC-1
LD B,0
CKFT1:PUSH DE
CKFT2:INC HL
LD A,(HL)
CP 255
JR Z,FTRET
AND 7FH
EX DE,HL
CP (HL)
EX DE,HL
JR NZ,NTHIS1
INC DE
BIT 7,(HL)
JR Z,CKFT2
LD A,B
FTRET:POP DE
POP BC
POP HL
RET

NTHIS1:INC B
POP DE
NTHIS1A:BIT 7,(HL)
JR NZ,CKFT1
INC HL
JR NTHIS1A

MSRC:DB "SR","C".S
MCOM:DB "CO","M".S
MDAT:DB "DA","T".S
MBAK:DB "BA","K".S
MCOP:DB "CO","P".S
MDBU:DB "DB","U".S
MBLD:DB "BL","D".S
DB 255

OPEN:LD C,OPE
JR BDOS
CLOSE:LD C,CLO
JR BDOS
ERASE:LD C,ERA
JR BDOS
CREATE:LD C,CRE
JR BDOS
RENAME:LD C,REN

BDOS:PUSH HL
PUSH DE
PUSH BC
LD DE,FCB
CALL 5
POP BC
POP DE
POP HL
RET

PRNAME:
LD HL,FCB+1
LD B,8
PRNAM1:LD A,(HL)
INC HL
CALL PRINT
DJNZ PRNAM1
LD A,'.'
CALL PRINT
LD B,3
PRNAM2:LD A,(HL)
INC HL
CALL PRINT
DJNZ PRNAM2
RET

GETCUR:
CALL TOP
GCUR:LD HL,(CURRENT)
LD DE,(LINEST)
SBC HL,DE
RET Z
RET C
CALL DLINE
JP Z,TOP
JR GCUR

SEND:
CALL SPRINT
DB 13,'START ',32.S
LD HL,(AMADD)
CALL PRINTHL
CALL SPRINT
DB ' LEN ',32.S
LD HL,(COUNT)
CALL PRINTHL
LD A,18H
CALL PRINT
LD A,(TYPE)
CP 'S'
JP Z,SSEND


CALL PUTPHEAD
LD HL,(ADD)
LD DE,(COUNT)
CALL PPBLOK
LD A,3
OUT (PCNT),A
RET

PPBLOK:LD A,(HL)
CALL PPBYTE
RET C
INC HL
DEC DE
LD A,D
OR E
JR NZ,PPBLOK
RET

PUTPHEAD:
CALL INITPIO
LD HL,HEAD
LD DE,8
JP PPBLOK


PPBYTE:
PUSH AF
XOR A
LD (INTFLAG),A
POP AF
OUT (PDATA),A
WAIT:IN A,(20H)
AND 20H
JR Z,ABORT
LD A,(INTFLAG)
OR A  
JR Z,WAIT
OR A
RET

ABORT:LD A,3
OUT (PDATA),A
SCF
RET

INITPIO:
LD A,0FH
DI
OUT (PCNT),A
LD A,87H
OUT (PCNT),A
LD HL,INTER
LD (INTVECT),HL
EI
RET

INTER:
PUSH HL
LD HL,INTFLAG
SET 0,(HL)
POP HL
EI
RETI

TYPE:DB 'S'

ADD:DW 0

INTFLAG:DB 0
HEAD:
HDAT:DB 1,1,1,0
AMADD:DW 0
COUNT:DW 0


PCN:EQU 11H
PDA:EQU 10H

SSEND:
CALL SPSENDHED
LD DE,(COUNT)
LD HL,(ADD)
LD A,D
OR E
RET Z
SS1:LD A,(HL)
CALL SSBYTE
RET C
INC HL
DEC DE
LD A,D
OR E
JR NZ,SS1
RET

SPSENDHED:
PUSH HL
LD HL,(COUNT)
LD A,H
OR L
JR Z,NOHEADER
LD A,3
CALL SSBYTE
CALL SENDHL
LD HL,(AMADD)
CALL SENDHL
LD HL,0FFFFH
CALL SENDHL
CALL SENDHL
NOHEADER:POP HL
RET

SENDHL:
LD A,L
CALL SSBYTE
LD A,H

SSBYTE:
PUSH AF
SSB1:IN A,(20H)
AND 20H
JR Z,SABORT
IN A,(PCN)
BIT 0,A
JR Z,SSB1
PUSH BC
LD B,80H
SDEL:PUSH BC
POP BC
DJNZ SDEL
POP BC
POP AF
OR A
OUT (PDA),A
RET
SABORT:POP AF
SCF
RET

SETUP:
LD A,40H
OUT (PCN),A
LD A,0CEH
OUT (PCN),A
LD A,27H
OUT (PCN),A
RET


CKCOLD:
LD HL,(SOF)
LD DE,(EOF)
OR A  
SBC HL,DE
RET NZ
LD HL,(LINEST)
LD BC,1
CALL MROOM
LD (HL),13
RET 

FND0D:LD A,(HL)
CP 13 
RET Z 
INC HL
JR FND0D

GBSIZE:
CALL GFBLOK
JP PO,NBLFN
LD (FBMARK),HL
DEC HL
LD A,(BMARK)
AND 3 
CP 2  
JR Z,BIGBLK
CALL GETLG
LD B,0
RET 

BIGBLK:
PUSH HL
INC HL
LD A,0E4H;  MARKER
CPIR 
POP DE
JP PO,NBLFN
CALL FND0D
PUSH DE
OR A  
SBC HL,DE
LD B,H
LD C,L
INC BC
POP HL
RET 

NBLFN:POP HL
JP ERR

GFBLOK:LD HL,(SOF)
LD BC,(EOF)
OR A  
SBC HL,BC
LD HL,(SOF)
LD A,0E4H;   MARKER
CPIR 
RET 

CLS:PUSH HL
PUSH BC
LD HL,3BFFH
CALL TOHL
LD BC,960
CLS1:LD A,32
OUT (VDATA),A
DEC BC
LD A,B
OR C
JR NZ,CLS1
LD (VCOL),BC
POP BC
POP HL
RET

SCRUP:LD B,A
OR A
JR Z,SCRUP1
LD HL,3C00H
LD DE,40
SCRUP2:ADD HL,DE ;POINT TO NEXT
CALL INBUFF
OR A
SBC HL,DE ;BACK ONE
CALL OUTBUF
ADD HL,DE ;BACK TO ORIGINAL
DJNZ SCRUP2
SCRUP1:LD B,40
LD A,32
CALL TOHL
SCRUP3:OUT (VDATA),A
DJNZ SCRUP3
RET

SCRDWN:
; SCROLL DOWN FROM
; LINE IN A

LD C,A
LD DE,40
LD A,23
SUB C
LD HL,3C00H+880
OR A  
JR Z,SCRD4
LD B,A
SCRD3:CALL INBUFF
ADD HL,DE
CALL OUTBUF
OR A
SBC HL,DE
SBC HL,DE
DJNZ SCRD3
SCRD4:ADD HL,DE
DEC HL
CALL TOHL
LD B,40
LD A,32
SCRDW1:OUT (VDATA),A
PUSH AF
POP AF
DJNZ SCRDW1
RET

INBUFF:PUSH BC
PUSH HL
CALL TOHL
LD C,VDATA
LD B,E
LD HL,INCLBUF
INBUF1:INIR
POP HL
POP BC
RET

OUTBUF:PUSH BC
PUSH HL
DEC HL
CALL TOHL
LD C,VDATA
LD B,E
LD HL,INCLBUF
OTBUF1:OTIR
POP HL
POP BC
RET


SET40C:
LD A,3
LD B,80H
LD HL,INVDP
SETV1:LD C,(HL)
CALL TOBC
INC B
INC HL
DEC A
JR NZ,SETV1
RET

INVDP:DB 0,0D0H,0FH

FIND:
LD HL,(LINEST)
PUSH HL
LD HL,(LFIND)
LD A,(STRLG)
OR A
JR Z,NFIND1
FIND1:LD DE,FINDB
LD A,(STRLG)
LD B,A
PUSH HL
FIND3:LD A,(HL)
CP 13 
JR Z,NFIND
LD A,(DE)
CP "*"
JR Z,ISWILD
CP (HL)
ISWILD:
INC HL
INC DE
JR NZ,NFIND
DJNZ FIND3
LD (LFIND),HL
POP HL
POP BC
RET

NFIND:POP HL
LD A,(HL)
INC HL
CP 13
JR NZ,FIND1
CALL DLINE
LD HL,(LINEST)
JR NZ,FIND1
NFIND1:POP HL
LD (LINEST),HL
OR 1
RET

CNGLN:
INC C
DEC C
RET Z
LD C,0
PUSH AF
LD DE,BUFF2
LD HL,BUFF
LD A,(HL)
CP MARKER
LD A,0E4H
CALL Z,ADFB
INC HL
LD A,(HL)
CP 32
JR Z,NBLAB
CP ";"
JR Z,GOTOP1
CP 13
JR Z,GOTLN
LD B,9
GETBL1:LD A,(HL)
CP ';'
JR Z,GOTLB
CP 13 
JR Z,GOTLB
INC HL
CP 32
JR Z,GOTLB
CALL ADFB
DJNZ GETBL1
GETLB3:LD A,(HL)
CP ";"
JR Z,GOTLB
CP 32 
JR Z,GOTLB
CP 13
JR Z,GOTLB
INC HL
JR GETLB3
GOTLB:LD A,":"
CALL ADFB
LD A,(HL)
CP ';'
JR Z,GOTOP1
NBLAB:CALL FNSP
NBLAB1:LD B,4
GOTLB1:LD A,(HL)
CP ";"
JR Z,GOTOP1
INC HL
CP 13
JR Z,GOTLN
CP 32
JR Z,GOTOPC
CALL ADFB
DJNZ GOTLB1
GOTOPC:CALL FNSP
LD A,32
CALL ADFB
GOTOP1:LD A,(HL)
INC HL
CP 13
JR Z,GOTLN
CALL ADFB
JR GOTOP1
GOTLN:LD A,13
CALL ADFB
LD A,C
LD HL,(LINEST)
CALL GETLG
LD DE,BUFF2

;A =NEW LG
;C =OLD LG
;DE=NEW
;HL=OLD

CALL XCNGLN
LD HL,(VCOL)
PUSH HL
CALL PRLINE
POP HL
LD (VCOL),HL
POP AF
RET

XCNGLN:CP C
PUSH AF
JR Z,SAMEL
JR NC,NILNGR
SUB C
NEG
LD C,A
LD B,0
CALL DROOM
JR SAMEL
NILNGR:SUB C
LD C,A
LD B,0
CALL MROOM
SAMEL:POP BC
LD C,B
LD B,0
EX DE,HL
LDIR
RET 

FSP:LD A,(HL)
CP 32
RET Z
CP 0DH
RET Z 
INC HL
JR FSP

FNSP:
LD A,(HL)
CP 32
RET NZ
INC HL
JR FNSP

GETLG:
PUSH HL
PUSH AF
LD C,0
GETLG1:LD A,(HL)
INC HL
INC C
CP 13
JR NZ,GETLG1
POP AF
POP HL
RET

ADFB:
LD (DE),A
INC DE
INC C
RET

MAIN:CALL CKCOLD
CALL GETLN
MAIN1:CP 27
CALL NZ,CNGLN
MAIN2:LD DE,(VCOL)
LD HL,MRET
PUSH HL
LD (ERSP),SP
CP 32
JP NC,ERR
LD HL,ACTAB
ADD A,A
ADD A,L
LD L,A
JR NC,NC1
INC H
NC1:LD A,(HL)
INC HL
LD H,(HL)
LD L,A
JP (HL)


ISPOINTER:
LD HL,FLAGS
BIT 4,(HL);SET IF GRF PRESD
JR NZ,SETPNT
CALL GETZKEY
CALL GETPNT
LD C,(HL)
INC HL
LD B,(HL)
LD (CURRENT),BC
CALL GETCUR
CALL DRAWSCR
RET

SETPNT:
CALL GETZKEY
CALL GETPNT
LD DE,(LINEST)
LD (HL),E
INC HL
LD (HL),D
RET

GETPNT:
LD HL,PNTADS
AND 07H
ADD A
ADD L
LD L,A
RET NC
INC H
RET

MRET:
JR MAIN

ACTAB:
DW ISPOINTER
DW CNTA
DW CNTB
DW CNTC
DW CNTD
DW CNTE
DW CNTF
DW CNTG
DW CNTH
DW CNTI
DW CNTJ
DW CNTK
DW CNTL
DW CNTM
DW CNTN
DW CNTO
DW CNTP
DW CNTQ
DW CNTR
DW CNTS
DW CNTT
DW CNTU
DW CNTV
DW CNTW
DW CNTX
DW CNTY
DW CNTZ
DW ESC
DW F3
DW ERR
DW ERR
DW ERR

ERR:
LD HL,(VCOL)
PUSH HL
LD L,0
LD (VCOL),HL
CALL SPRINT
DB ' ERROR'
DB '!'.S
CALL GETZKEY
CALL PRLINE
POP HL
LD (VCOL),HL
RET

CNTA:EQU ERR
CNTB:
LD A,(BMARK)
OR A
JP NZ,ERR
CNTBX:INC A
LD (BMARK),A
LD HL,(LINEST)
LD BC,1
CALL MROOM
LD (HL),0E4H ; MARKER
CALL PRLINE
RET

CNTC:
LD A,(BMARK)
CP 2
JP NC,ERR
OR 128
JP CNTBX

CNTD:
LD A,(BMARK)
OR A  
JR NZ,BDEL
LD HL,(LINEST)
CALL GETLG
LD B,0

PUSH HL
PUSH BC
INC BC
LD DE,BLDBUFF
LDIR 
POP BC
POP HL
LD A,1
LD (DELFLAG),A

CALL DROOM
CALL ULINE
JP Z,CNTT
CALL DLINE
JR NZ,CNTD0
JOINENT:LD A,(VROW)
OR A  
JR Z,CNTD0
DEC A 
LD (VROW),A
CNTD0:LD HL,(VCOL)
PUSH HL
LD A,(VROW)
OR A
JP Z,CNTD2
LD B,A
CNTD1:PUSH BC
CALL ULINE
POP BC
DJNZ CNTD1
CNTD2:CALL HOME
CALL DRAWSC0
POP HL
LD (VCOL),HL
LD B,H
LD A,H
OR A
RET Z
CNTD3:PUSH BC
CALL DLINE
POP BC
DJNZ CNTD3
RET

BDEL:
CP 1
JP NZ,ERR

PUSH AF
CALL SPRINT
DB 13,32,15H,'BLOCK DEL'
DB 'ETE (Y/N',')'.S
BDWAIT:CALL GETZKEY
CP 'N'
JR Z,NBLKDEL
CP 'Y'
JR NZ,BDWAIT
POP AF

CALL CNTBX
CALL GBSIZE
CALL DROOM
LD HL,(FBMARK)
LD (CURRENT),HL
CALL GETCUR
CALL CNTZ
RET 

NBLKDEL:POP AF
JP ESC


CNTE:
CALL TOP
CNTE1:CALL HOME
CALL DLINE
JR NZ,CNTE1
LD B,23
CNTE2:PUSH BC
CALL ULINE
POP BC
DJNZ CNTE2
CALL HOME
JP DRAWSCR

CNTF:
CALL FIND
CALL NZ,ULINE
JP DRAWSCR

CNTG:EQU ERR

COPY:
CALL GBSIZE
PUSH HL
LD DE,(LINEST)
OR A
SBC HL,DE
POP HL
JR NC,COPYOK
ADD HL,BC
OR A
SBC HL,DE
JR Z,COPYOK
JR NC,COPYERR
COPYOK:EX DE,HL
CALL MROOM
PUSH HL
PUSH BC
CLRHOLE:LD (HL),0
INC HL
DEC BC
LD A,B
OR C
JR NZ,CLRHOLE
CALL GBSIZE
POP BC
POP DE
PUSH BC
LDIR 
POP BC
OR A  
SBC HL,BC
RET 

COPYERR:POP HL
JP ERR

CNTH:
LD A,(BMARK)
AND 0F0H
JP Z,ERR
CP 128
JR Z,COPY1
CALL COPY
CALL DROOM
CALL CNTZ
RET 

COPY1:
CALL COPY
CALL CNTZ
RET 

NBCOP:JP ERR

CNTI:
; INSERT
LD A,20
LD (0FB41H),A
CALL NEWLINE
GETNL:LD C,255
CALL GETLN1
CP 13
JR Z,NLOK
CP 27
JR Z,GETNL

NLOK:
PUSH AF
CALL CNGLN
POP AF
CP 13
JR Z,NLOK1
PUSH AF
CALL SETKEYS
POP AF
POP HL
JP MAIN1

NLOK1:CALL PRLINE
CALL DLINE
LD A,(VROW)
CP 23 
JR Z,CNTI2
CALL PRLF
JR CNTI
CNTI2:CALL SCRUP
JR CNTI

PAGE:
LD A,(VROW)
OR A
JP Z,DRAWSCR
LD B,A
PAGE1:PUSH BC
CALL ULINE
POP BC
DJNZ PAGE1
CALL HOME
JP DRAWSCR

NEWLINE:
LD A,(VROW)
CALL SCRDWN
NEWL1:LD HL,(LINEST)
LD BC,1
CALL MROOM
LD (HL),13
CALL PRCR
LD A,15H
CALL PRINT
RET

CNTL:EQU ERR
CNTM:
LD HL,FLAGS
BIT 5,(HL)
JR Z,NMOVE
LD A,(BMARK)
BIT 1,A
JP NZ,ERR
OR A  
JP M,ERR
OR 64 
JP CNTBX

NMOVE:
CALL NOJOIN
CALL NEWLINE
LD A,1
LD (VCOL),A
RET 

CNTN:EQU ERR
CNTO:
LD A,(DELFLAG)
OR A
JP Z,ERR
LD A,(VROW)
CALL SCRDWN
LD HL,BLDBUFF
CALL GETLG
LD B,0
LD HL,(LINEST)
CALL MROOM
LD DE,BLDBUFF
EX DE,HL
LDIR 
PUSH DE
CALL ESC
LD DE,(VCOL)
CALL NOJOIN
POP HL
LD (LINEST),HL
JP ESC


CNTP:
LD A,(BMARK)
CP 2
JP NC,ERR
CALL CNTBX
CALL GBSIZE
CALL CNTLINES
PUSH BC
CALL CNTZ
CALL TOP
LD HL,(FBMARK)
LD (CURRENT),HL
CALL GETCUR
CALL ULINE
CALL DRAWSCR
CALL PEOLBUF
LD DE,(FRMFEED)
POP BC
POUTLN:PUSH BC
PUSH DE
CALL PR1LINE
LD DE,(VCOL)
PUSH AF
CALL NOJOIN
POP AF
POP DE
DEC E
JR NZ,NFRMFED
;  LD   A,0CH
;  CALL POUT
LD DE,(FRMFEED)
NFRMFED:POP BC
JP C,ESC
DEC BC
LD A,B
OR C
JR NZ,POUTLN
JP ESC

CNTLINES:
LD HL,(FBMARK)
LD DE,0
CNTLIN1:LD A,(HL)
INC HL
CP 0DH
JR NZ,NLINEH
INC DE
NLINEH:DEC BC
LD A,B
OR C
JR NZ,CNTLIN1
LD B,D
LD C,E
RET

OUTLINE:
PR1LINE:
PUSH BC
CALL SCRADD
;   DEC  HL
CALL TOHL
LD B,40
PR1CHR:IN A,(VDATA)
CALL POUT
JP C,PR1CHR2
DJNZ PR1CHR
POP BC

PEOLBUF:PUSH BC
LD HL,EOLBUF
PR1LN1:LD A,(HL)
INC HL
CP 32
JR Z,DUNEOL
CALL POUT
JR PR1LN1
DUNEOL:OR A
PR1CHR2:POP BC
RET
O4HX:
LD A,H
CALL O2HX
LD A,L

O2HX:PUSH AF
RRA
RRA
RRA
RRA
CALL OHX
POP AF

OHX:AND 0FH
ADD 90H
DAA
ADC 40H
DAA


POUT:
PUSH HL
LD HL,BUSY
PUSH AF
PO1:BIT 0,(HL)
JR Z,PIO2
IN A,(20H)
AND 20H
JR NZ,PO1
POP AF
SCF
POP HL
RET
PIO2:POP AF
OUT (30H),A
SET 0,(HL)
OR A
POP HL
RET

CNTQ:
LD A,(VROW)
LD B,A
LD A,24
SUB B
LD B,A
F1AA:PUSH BC
CALL DLINE
POP BC
JP Z,DRAWSCR
DJNZ F1AA
JP DRAWSCR


CNTR:EQU ERR

CNTS:
LD HL,(LINEST)
CNTS1:LD A,(HL)
INC HL
CP 0DH
JP Z,CNTI
CP ":"
JR NZ,CNTS1
PUSH HL
LD A,(VROW)
CALL SCRDWN
POP HL
LD BC,1
CALL MROOM
LD (HL),13
PUSH HL
CALL ESC
LD DE,(VCOL)
CALL NOJOIN
POP HL
INC HL
LD (LINEST),HL
JP ESC

TOP:
LD HL,(SOF)
LD (LINEST),HL
RET

CNTT:
CALL TOP
CALL DRAWSCR
RET

CNTV:LD HL,(LINEST)
CALL VIEW
JP DRAWSCR

CNTW:
LD A,(VROW)
OR A
JR Z,F2A
LD B,A
F2AA:PUSH BC
CALL ULINE
POP BC
JP Z,CNTT
DJNZ F2AA
F2A:LD B,24
F2B:PUSH BC
CALL ULINE
POP BC
JP Z,CNTT
DJNZ F2B
JP DRAWSCR

CNTX:EQU ERR
CNTY:EQU ERR

CNTZ:
LD BC,(SOF)
LD HL,(EOF)
OR A  
SBC HL,BC
LD B,H
LD C,L
LD HL,(SOF)
CNTZ1:LD A,0E4H; MARKER
CPIR 
JP PO,CNTZ2
DEC HL
PUSH BC
LD BC,1
CALL DROOM
POP BC
INC BC
JR CNTZ1
CNTZ2:XOR A
LD (BMARK),A
LD HL,(LINEST)
LD (CURRENT),HL
LD BC,(VCOL)
PUSH BC
CALL GETCUR
INC B
DEC B
JR Z,NULINE
CNTZ2A:PUSH BC
CALL ULINE
POP BC
DJNZ CNTZ2A
NULINE:CALL DRAWSCR
POP BC
LD A,C
LD (VCOL),A
INC B
DEC B
RET Z
CNTZ3:PUSH BC
LD DE,(VCOL)
CALL NOJOIN
POP BC
DJNZ CNTZ3
RET 


DROOM:
PUSH HL
PUSH DE
PUSH BC
PUSH HL
LD A,8
DROOM1:PUSH AF
DEC A
CALL GETPNT
LD E,(HL)
INC HL
LD D,(HL)
POP AF
EX (SP),HL
CALL UPDATEDEL
EX (SP),HL
LD (HL),D
DEC HL
LD (HL),E
DEC A
JR NZ,DROOM1
POP HL
LD DE,(EOF)
EX DE,HL
OR A
SBC HL,BC
LD (EOF),HL
OR A
SBC HL,DE
JR Z,NDROOM
PUSH HL
LD H,D
LD L,E
ADD HL,BC
POP BC
LDIR
NDROOM:LD HL,(EOF)
LD (HL),1AH
POP BC
POP DE
POP HL
RET

F1:EQU ERR

F2:EQU ERR



COMLF:PUSH HL
LD HL,VCOL+1
LD A,(HL)
CP 5
JR Z,SCRREQ
INC (HL)
POP HL
RET
SCRREQ:CALL SCRUP  ;A=5
POP HL
RET

F3:
CMNDAGAIN:
LD (ERSP),SP
CALL COPYC]S
CALL PRCR
LD A,">"
CALL PRINT
GETCOM:
CALL GETLN
CP 27
JP Z,COMRET
CP 11
JR NZ,NULC
LD HL,VCOL+1
DEC (HL)
JP P,GETCOM
INC (HL)
JR GETCOM
NULC:CP 10
JR NZ,NDLC
CALL COMLF
JR GETCOM
NDLC:CP 13
JR NZ,GETCOM
LD A,1
LD (VCOL),A
LD HL,BUFF+1
CALL FNSP
PUSH HL
LD HL,COMTAB-2
GETCOMLP:INC HL
INC HL
BIT 7,(HL)
JR NZ,BADCOM0
CP (HL)
INC HL
JR NZ,GETCOMLP
LD A,(HL)
INC HL
LD H,(HL)
LD L,A
EX (SP),HL
INC HL
CALL FNSP
CALL COMLF
CALL COPYS]C
RET

BADCOM0:POP HL
BADCOM:CALL SPRINT
DB '?BAD COMMAN',"D".S
CALL COMLF
JR CMNDAGAIN

COMTAB:
DB 13
DW COMRET
DB "A"
DW AC
DB "B"
DW BCOM
DB "C"
DW CCOM
DB "D"
DW DC
DB "E"
DW EC
DB "F"
DW FC

DB "H"
DW HC
DB "I"
DW ICOM

DB "K"
DW KC
DB "L"
DW LC
DB "M"
DW MC
DB "O"
DW OC

DB "S"
DW SC
DB "T"
DW TC
DB "V"
DW VC
DB "X"
DW XC
DB "W"
DW WHO
DB 23H
DW C23H
DB 255

WHO:
LD A,(HL)
CP "H"
JR NZ,BADCOM
INC HL
LD A,(HL)
CP "O"
JR NZ,BADCOM
INC HL
LD A,(HL)
CP 13
JR NZ,BADCOM
CALL CLS
LD A,16
CALL PRMES
CALL GETZKEY
JP CMNDAGAIN

VC:LD DE,VIEWBUFF
LD BC,16
LDIR 
LD A,13
LD (VIEWBUFF+15),A
LD HL,(SOF)
CALL VIEW
JP Z,CMNDAGAIN
JP DRAWSCR

VIEW:
LD DE,VIEWBUFF
CALL FND0D
EX DE,HL
NXTLINE:LD (CURRENT),DE
VIEW1:LD A,(DE)
INC DE
CP 1AH
RET Z
CP 13
JR Z,NXTLINE
CP ":"
JR NZ,VIEW1
PUSH HL
LD DE,(CURRENT)
EX DE,HL
MATCH:LD A,(DE)
CP 13
JR Z,GOTMATCH
CP (HL)
INC HL
INC DE
JR Z,MATCH
CALL FND0D
POP DE
EX DE,HL
JR NXTLINE
GOTMATCH:POP HL
CALL GETCUR
OR 1
RET


COMRET:CALL COPYS]C
JP DRAWSCR

FC:LD BC,15
LD DE,FINDB
LDIR
EX DE,HL
LD B,15
F3F1:DEC HL
LD A,(HL)
CP 13 
JR Z,F3F1A
CP 32 
JR NZ,F3F3
F3F1A:DJNZ F3F1
F3F3:INC HL
LD (HL),13
LD HL,(LINEST)
LD (LFIND),HL
LD HL,FINDB
CALL GETLG
DEC C 
LD A,C
LD (STRLG),A
JP CNTF

CCOM:PUSH HL
LD DE,30
ADD HL,DE
LD B,30
F3C1:DEC HL
LD A,(HL)
CP 13 
JR Z,F3C1A
CP 32 
JR NZ,F3C3
F3C1A:DJNZ F3C1
F3C3:INC HL
LD (HL),13
LD HL,(LINEST)
LD (LFIND),HL
POP HL
CALL GETLG
DEC C 
JR Z,ABORTCX
XOR A 
LD (ALL),A
EX DE,HL
F3C4:PUSH DE
PUSH BC
F3C5:
CALL FIND
JR NZ,ABORTCG
LD A,(ALL)
OR A  
JR NZ,CNGOK
PUSH HL
CALL CLS
LD A,1
LD (VROW),A
CALL DRAWSC0
CALL DLINE
CALL HOME
CALL SPRINT
DB 'CHANGE? (Y/N/A/X)'
DB 21+80H
POP HL
CNGASK:CALL GETZKEY
CP "N"
JR Z,F3C5
CP "X"
JR Z,ABORTCG
CP "Y"
JR Z,CNGOK
CP "A"
JR NZ,CNGASK
LD (ALL),A
CNGOK:
POP BC
POP DE
PUSH DE
PUSH BC
LD A,(STRLG)
LD B,A
LD A,C
LD C,B
CALL XCNGLN
POP BC
POP DE
OR 1  
JR F3C4
ABORTCG:POP BC
POP BC
ABORTCX:CALL ULINE
CALL HOME
JP DRAWSCR


OC:CALL COMLF
LD A,(HL)
CP 'S'
JR Z,ISTYPE
CP 'P'
JR NZ,NOTYPE
ISTYPE:INC HL
LD (TYPE),A
NOTYPE:CALL FNSP
CP 13 
JR Z,F301
EX DE,HL
DW GETHL
LD (ADD),HL
EX DE,HL
CALL FNSP
CP 13 
JR Z,F301
EX DE,HL
DW GETHL
LD (AMADD),HL
EX DE,HL
CALL FNSP
CP 13 
JR Z,F301
EX DE,HL
DW GETHL
LD (COUNT),HL
F301:CALL SEND
JP CMNDAGAIN

NAMEAD:DW 0

BCOM:LD A,0
LD (DELFLAG),A
LD (NAMEAD),HL
CALL GETNAM
CALL SETBLD
CALL CLS
CALL OPEN
INC A 
JR Z,NOBLDERA
CALL SPRINT
DB 'ERASING ',32.S
CALL PRNAME
CALL PRCRLF
CALL ERASE
CALL CLOSE
NOBLDERA:CALL CREATE
CALL SPRINT
DB 'TYPE FILE AS YOU '
DB 'DEFINE KEYS',13,10
DB 'I.E. ENCLOSE '
DB 'IN QUOTES',13,10
DB 'MAX LENGTH IS 128'
DB ' CHARACTERS',13,10.S

GETBLD0:CALL GETLN
CP 13
JR NZ,GETBLD0
LD HL,BUFF
CALL FNSP
INC HL
LD C,A
LD B,32
CP "'"
JR Z,ISQUOTE
CP '"'
JP NZ,BLDERR
ISQUOTE:
LD DE,BLDBUFF
CALL GETKEYDEF
CP C
JR Z,GETBLD3

LD B,39
GETBLD1:PUSH DE
PUSH BC
CALL PRCRLF
GETBLD2:CALL GETLN
CP 13
JR NZ,GETBLD2
POP BC
POP DE
LD HL,BUFF
CALL GETKEYDEF
CP C
JR NZ,GETBLD1
GETBLD3:
INC E
JP P,BLDERR
DEC E
JR Z,BLDERR
LD C,E
LD B,0
LD C,E
RES 7,C
LD HL,BLDBUFF
LD DE,DMA
LDIR 
LD A,1AH
LD (DE),A
CALL WRITEB
CALL CLOSE
JP CMNDAGAIN

BLDERR:CALL CLS
CALL SPRINT
DB 'BUILD ERROR',10,13.S
CALL GETZKEY
JP CMNDAGAIN


EC:LD A,0
LD (DELFLAG),A
LD (NAMEAD),HL
CALL GETNAM
CALL SETBLD
CALL OPEN
INC A 
LD A,1
JP Z,EXECERR
LD DE,FCB
LD C,RSEQ
CALL 5
OR A
LD A,3
JR NZ,EXECERR
LD DE,BLDBUFF
LD (BUILDAD),DE
LD HL,DMA
LD BC,128
LDIR 
LD A,255
LD (BLDFLAG),A
JP CMNDAGAIN

EXECERR:
PUSH AF
CALL CLS
POP AF
JP SERR


SC:LD (NAMEAD),HL
CALL GETNAM
CALL CLS
CALL OPEN
INC A 
JP Z,F3S2
CALL CLOSE
CALL SETBAKUP
CALL OPEN
INC A 
JR Z,F3S1
CALL SPRINT
DB 'ERASING ',32.S
CALL PRNAME
CALL PRCRLF
CALL ERASE
CALL CLOSE
F3S1:CALL GETNAM
CALL OPEN
INC A
JR Z,F3S2
CALL SPRINT
DB 'RENAMING',32.S
CALL PRNAME
CALL SPRINT
DB 13,10
DB 'TO      ',32.S
LD DE,FCB+11H
LD HL,FCB+1
LD BC,11
LDIR 
CALL SETBAKUP
CALL PRNAME
LD HL,FCB+9
LD DE,FCB+19H
LD B,3
REN1:LD A,(DE)
LD C,(HL)
LD (HL),A
LD A,C
LD (DE),A
INC DE
INC HL
DJNZ REN1
CALL PRCRLF
CALL RENAME
F3S2:CALL CLOSE
CALL GETNAM
PUSH DE
OR A
SBC HL,DE
PUSH HL
CALL OPEN
INC A 
LD A,4
JR NZ,SERR
CALL CREATE
INC A 
LD A,14
JR Z,SERR
CALL SPRINT
DB 'SAVING  ',32.S
CALL PRNAME
CALL PRCRLF
POP BC
POP HL
SSRC:LD DE,DMA
LD A,128
SSRC1:LDI
JP PO,SAVDALL
DEC A
JR NZ,SSRC1
CALL WRITEB
OR A
LD A,2
JR NZ,SERR
JR SSRC
SAVDALL:
CALL WRITEB
OR A
LD A,2
JR NZ,SERR
CALL CLOSE
INC A
LD A,3
JP Z,SERR
CALL PRINTOK
CALL TOP
JP CMNDAGAIN

PRINTOK:CALL PRCRLF0
CALL SPRINT
DB 'O.K.',13,10.S
CALL GETZKEY
RET

PRCRLF0:LD A,(VCOL)
CP 2
RET C
JP PRCRLF

SERR:CALL PRMES
LD SP,(ERSP)
CALL GETZKEY
JP CMNDAGAIN

XC:LD DE,(EOF)
JR APEND
LC:LD DE,(SOF)
APEND:PUSH DE
LD (NAMEAD),HL
CALL GETNAM
PUSH DE
CALL CLS
CALL SPRINT
DB 'LOADING ',32.S
CALL PRNAME
CALL PRCRLF
CALL OPEN
INC A 
LD A,1
JR Z,SERR
LD DE,FCB+9
CALL CKFTYPE
CP BAK
JR Z,LDSRC
CP SRC
JR NZ,ISCOMF
LDSRC:POP HL
POP HL
F3L2:LD DE,FCB
LD C,RSEQ
PUSH HL
CALL 5
POP HL
OR A
LD A,3
JR NZ,SERR
LD DE,DMA
LD B,128
F3L3:LD A,(DE)
INC DE
LD (HL),A
CP 0AH
JR Z,F3L5
CP 1AH
JR Z,F3L4
INC HL
F3L5:DJNZ F3L3
JR F3L2
F3L4:LD (EOF),HL
LD (HL),1AH
DUNLOAD:CALL CLOSE
CALL PRINTOK
CALL TOP
JP CMNDAGAIN

ISCOMF:POP HL
LD A,H
OR L
JR Z,DUNLOAD
POP DE
LCOM1:LD DE,FCB
LD C,RSEQ
PUSH HL
CALL 5
POP HL
OR A
PUSH AF
LD DE,DMA
LD BC,128
EX DE,HL
LDIR 
EX DE,HL
POP AF
JR Z,LCOM1
JR DUNLOAD


DC:
LD (0E349H),HL
CALL FND0D
LD (HL),0
CALL CLS
CALL PRCRLF
CALL 0E6BBH
CALL GETZKEY
JP CMNDAGAIN

AC:CALL CLS
CALL SPRINT
DB 22,0,15
DB 'ASSEMBLING',10,10
DB 13.S
CALL ASMBL
CALL SETKEYS
LD DE,(PC)
LD HL,(AMADD)
CALL SPRINT
DB 'OBJECT START ',32.S
CALL PRINTHL
CALL PRCRLF
EX DE,HL
DEC HL
CALL SPRINT
DB 'OBJECT END   ',32.S
CALL PRINTHL
CALL PRCRLF
OR A
SBC HL,DE
CALL SPRINT
DB 'OBJECT LENGTH',32.S
CALL PRINTHL
CALL GETZKEY
JP CMNDAGAIN


MC:LD A,C40
DW OUTC
LD C,26H
CALL 5


HC:CALL CLS
CALL SPRINT
DB 'SOURCE SIZE'
DB 13,10
DB 'START ',32.S
LD HL,(SOF)
CALL PRINTHL
CALL SPRINT
DB 13,10,'END   ',32.S
EX DE,HL
LD HL,(EOF)
CALL PRINTHL
CALL SPRINT
DB 13,10,'LENGTH',32.S
OR A
SBC HL,DE
CALL PRINTHL
CALL GETZKEY
JP CMNDAGAIN

PRINTHL:CALL PRLNUM
CALL SPRINT
DB 32,32.S
PUSH HL
PUSH DE
DW ZDEC
POP DE
POP HL
RET

KC:CALL GETKEY
JP C,ERR
CALL SETKEYS
JP CMNDAGAIN

GETKEY:
CALL FNSP
SUB '0'
RET C
CP 8
CCF
RET C
ADD A,A
ADD A,A
ADD A,A
ADD A,A
LD DE,K0
ADD A,E
LD E,A
JR NC,$+3
INC D
INC HL
CALL FNSP
INC HL
LD C,A
LD B,16
CP "'"
JR Z,F3K0
CP '"'
JR Z,F3K0
SCF
RET

F3K0:CALL GETKEYDEF

DEC DE
LD A,(DE)
OR S
LD (DE),A
RET

GETKEYDEF:
LD A,(HL)
CP 13
JR Z,F3K1
CP '|'
JR NZ,F3K2
INC HL
LD A,(HL)
SUB "A"-1
F3K2:CP C
JR Z,F3K1
LD (DE),A
INC DE
DEC B
JR Z,F3K1
INC HL
JR GETKEYDEF
F3K1:
RET


C23H:
LD DE,VCOL
EX DE,HL
LD (HL),18
INC HL
DEC (HL)
EX DE,HL
CALL GETLAB
CALL PRINTHL
CALL COMLF
CALL COPYS]C
JP CMNDAGAIN


GETLAB:
EXX
LD D,255
LD E,40H
EXX
LD D,H
LD E,L
LD C,255
BCKLAB1:LD A,(HL)
INC HL
INC C
CP ' '
JR Z,GETLAB1
CP 13
JR NZ,BCKLAB1
GETLAB1:CALL MSYMSCH
PUSH AF
LD A,(HL)
INC HL
LD H,(HL)
LD L,A
POP AF
RET NC
LD HL,0
RET

ICOM:EX DE,HL
DW GETHL
CALL FSETUP
LD DE,DMA
LD B,9
GET01:CALL FETB
JR C,FABORT
DJNZ GET01
LD A,(DMA)
CP 3
JR Z,TYPEOK
CALL SPRINT
DB 13
DB ' WRONG FILE TYPE'
DB 32.S
JP DUNINPU
TYPEOK:LD BC,(DMA+1)
EX DE,HL
FLOOP:CALL FETB
JR C,FABORT
DEC BC
LD A,B
OR C
JR NZ,FLOOP
DUNINPU:CALL GETZKEY
JP CMNDAGAIN

FSETUP:
;B8   CE27
LD A,40H
OUT (PCNT),A
LD A,0CEH
OUT (PCNT),A
LD A,27H ;35
OUT (PCNT),A
RET
FETB:
FWAIT:IN A,(PCN)
RRA
RRA
JR C,FGOT
IN A,(20H)
AND 20H
JR NZ,FWAIT
SCF
RET
FGOT:IN A,(PDA)
LD (DE),A
INC DE
OR A
RET

WRITEB:
PUSH BC
LD DE,FCB
LD C,WSEQ
CALL BDOS
LD DE,DMA
POP BC
RET

FABORT:LD A,7
DW OUTC
CALL GETZKEY
JP 100H


TC:LD A,(HL)
LD (ALL),A
LD B,26
LD DE,(EOF)
LD (CURRENT),DE
LD (LINEST),DE
DO1LET:LD HL,LABADS
LD A,26
SUB B
ADD A
ADD L
LD L,A
ADC H
SUB L
LD H,A
DO1LAB:LD A,(HL)
INC HL
OR (HL)
JR Z,DUN1LET
LD A,(HL)
DEC HL
LD L,(HL)
LD H,A
LD A,"A"+26
SUB B
LD (DE),A
INC DE
LD C,(HL)
INC HL
DEC C
DEC C
DEC C
PUSH BC
JR Z,DUNPL
LD B,0
LDIR 
DUNPL:PUSH HL
LD HL,EQUMES
LD BC,5
LDIR 
POP HL
LD A,(ALL)
CP "1"
JR Z,DODEC
INC HL
PUSH HL
LD B,2
PUT2DIG:LD A,(HL)
SRL A
SRL A
SRL A
SRL A
CALL PUTDIG
LD A,(HL)
CALL PUTDIG
DEC HL
LD A,(HL)
OR A
JR NZ,$+3
INC C
DJNZ PUT2DIG
LD A,"H"
LD (DE),A
INC DE
POP HL
INC HL
JR DUNNUM
DODEC:PUSH HL
LD A,(HL)
INC HL
LD H,(HL)
LD L,A
EX AF,AF
OR A
EX AF,AF
LD BC,10000
CALL DODDIG
LD BC,1000
CALL DODDIG
LD BC,100
CALL DODDIG
LD BC,10
CALL DODDIG
LD A,L
ADD 30H
LD (DE),A
INC DE
POP HL
INC HL
INC HL
DUNNUM:LD A,0DH
LD (DE),A
INC DE
POP BC
JR DO1LAB
DUN1LET:DEC B
JP NZ,DO1LET
LD (EOF),DE
LD A,1AH
LD (DE),A
JP CMNDAGAIN

PUTDIG:AND 0FH
JR NZ,NL0
INC C
DEC C
RET Z
NL0:CP 10
JR C,NLL
INC C
DEC C
JR NZ,NLL
PUSH AF
LD A,"0"
LD (DE),A
INC DE
POP AF
NLL:ADD 30H
CP 30H+10
JR C,NOTLET
ADD 7
NOTLET:LD (DE),A
INC DE
INC C
RET

DODDIG:LD A,"0"
GETD1:OR A
SBC HL,BC
JR C,GOTDNUM
INC A
JR GETD1
GOTDNUM:ADD HL,BC
CP "0"
JR NZ,NLD0
EX AF,AF
JR C,DNUMFND
EX AF,AF
RET

DNUMFND:
EX AF,AF
NLD0:LD (DE),A
INC DE
EX AF,AF
SCF
EX AF,AF
RET

EQUMES:DB ":EQU "


ESC:CALL PRLINE
RET
CNTU:LD HL,(LINEST)
CALL FND0D
DEC HL
LD A,(HL)
CP ":"
JP NZ,ERR
INC HL
INC HL
LD A,(HL)
CP 1AH
JP Z,ERR
DEC HL
LD (LINEST),HL
LD BC,1
CALL DROOM
CALL ULINE
CALL ULINE
CALL JOINENT
LD DE,(VCOL)

CNTJ:
NOJOIN:LD A,D
CP 23
JR NZ,DWN1
CALL DLINE
JR Z,SETVP
LD A,23
CALL SCRUP
CALL PRLINE
OR 1
RET
DWN1:CALL DLINE
JR Z,SETVP
PUSH AF
INC D
POP AF
JR SETVP
CNTK:
LD A,D
OR A
JR NZ,UP1
CALL ULINE
JR Z,SETVP
XOR A
CALL SCRDWN
CALL PRLINE
RET
UP1:CALL ULINE
DEC D

SETVP:LD (VCOL),DE
RET


MROOM:PUSH HL
PUSH BC
PUSH DE
PUSH HL
LD DE,(EOF)
CALL UPDATE
PUSH HL
LD HL,(HIMEM)
OR A
SBC HL,DE
JR C,NOMEM
LD HL,(SOF)
DEC HL
SBC HL,DE
JR NC,NOMEM
POP HL
LD (EOF),DE
EX DE,HL
LD (HL),1AH
OR A
SBC HL,DE
PUSH HL
LD HL,(EOF)
LD D,H
LD E,L
OR A
SBC HL,BC
POP BC
LDDR
POP HL
LD A,8
MROOM1:PUSH AF
DEC A
CALL GETPNT
LD E,(HL)
INC HL
LD D,(HL)
POP AF
EX (SP),HL
CALL UPDATE
EX (SP),HL
LD (HL),D
DEC HL
LD (HL),E
DEC A
JR NZ,MROOM1
POP DE
POP BC
POP HL
RET

NOMEM:CALL CLS
CALL SPRINT
DB "OUT OF MEMORY",10,13.S
CALL GETZKEY
JP ENTRY

UPDATE:PUSH HL
PUSH DE
SBC HL,DE
POP HL
JR Z,UPDATE0
JR NC,UPDATE1
UPDATE0:ADD HL,BC
UPDATE1:POP DE
EX DE,HL
RET

UPDATEDEL:
PUSH HL
PUSH DE
SBC HL,DE
POP HL
JR Z,UPDATE0D
JR NC,UPDATE1D
UPDATE0D:OR A
SBC HL,BC
UPDATE1D:POP DE
EX DE,HL
RET

DRAWSCR:CALL CLS
DRAWSC0:
LD A,(VROW)
SUB 24
NEG 
LD B,A
JR P1
PRSCR:CALL PRCRLF
P1:PUSH BC
CALL PRLINE
CALL DLINE
POP BC
LD A,(VROW)
JR Z,P2
DJNZ PRSCR
INC A 
P2:OR A
JR Z,DONE
LD B,A
P3:PUSH BC
CALL ULINE
POP BC
JR Z,DONE
DJNZ P3
DONE:
LD A,(VROW)
CP 23 
JR Z,DONE1
CALL SPRINT
DB 13,10,32,15H+80H
OR 1  
DONE1:LD HL,1
LD (VCOL),HL
RET

DLINE:LD HL,(EOF)
LD (HL),1AH
LD HL,(LINEST)
DL1A:LD A,(HL)
INC HL
CP 01AH
RET Z
CP 13
JR NZ,DL1A
LD A,(HL)
CP 1AH
RET Z
LD (LINEST),HL
OR 1
RET


ULINE:LD HL,(LINEST)
PUSH BC
LD BC,(SOF)
OR A
SBC HL,BC
JR Z,ATSOF
JR C,ATSOF
LD HL,(LINEST)
DEC HL
ULINE1:DEC HL
LD A,(HL)
CP 13
JR NZ,ULINE1
INC HL
PUSH HL
OR A
SBC HL,BC
POP HL
JR NC,ADOK
LD HL,(SOF)
ADOK:POP BC
LD (LINEST),HL
OR 1
RET

ATSOF:LD HL,(SOF)
LD (LINEST),HL
XOR A
POP BC
RET

PRLINE:LD A,(VCOL)
PUSH AF
CALL PRCR
LD HL,(LINEST)
LD A,(HL)
INC HL
CP 0E4H ; MARKER
LD A,MARKER
JR Z,ISMARK
DEC HL
LD A,32
ISMARK:CALL PRINT
LD A,(HL)
CP ";"
JR Z,LINEND
LD B,32
PUSH HL
CKLAB:LD A,(HL)
CP 32
JR Z,NOLABLE
CP 13
JR Z,NOLABLE
CP ":"
JR Z,ISLABLE
CP ";"
JR Z,NOLABLE
INC HL
DJNZ CKLAB
NOLABLE:LD B,9
POP HL
JR DUNLAB
ISLABLE:POP HL
LD B,9
ISLAB1:LD A,(HL)
INC HL
CP ":"
JR Z,DUNLAB
CALL PRINT
DJNZ ISLAB1
ISLAB2:LD A,(HL)
INC HL
CP ':'
JR NZ,ISLAB2
DUNLAB:INC B
LABTAB:LD A,32
CALL PRINT
DJNZ LABTAB
LD B,5
OPCD:LD A,(HL)
CP 13
JR Z,LINEND
CP ';'
JR Z,LINEND
INC HL
CALL PRINT
CP 32
JR Z,OPCDUN
DJNZ OPCD
OPCDUN:LD A,(VCOL)
CP 16
JR Z,LINEND
LD A,32
CALL PRINT
JR OPCDUN
LINEND:LD A,(HL)
CP 13
JR NZ,NLIN
LD A,15H
NLIN:CALL PRINT
INC HL
LD A,(VCOL)
DEC A
JR NZ,LINEND
POP AF
LD (VCOL),A
RET
ISCOM:POP HL
JR LINEND


GETLN:LD C,0
GETLN1:CALL GETZKEY
LD HL,FLAGS
BIT 5,(HL)
JR NZ,GETLN2
CP 32
JR NC,ISASC
CP 25
JR Z,ISASC
CP 26
JR Z,ISASC
CP 4
JR Z,NOTASC
CP 8
JR Z,NOTASC
JR GETLN2
ISASC:LD C,255
NOTASC:CALL PRINT
JR GETLN1

GETLN2:PUSH AF
PUSH BC
LD A,(VROW)
ADD A,A
ADD A,A
ADD A,A
LD L,A
LD H,0
ADD HL,HL
ADD HL,HL
ADD A,L
LD L,A
JR NC,NC2
INC H
NC2:
LD BC,3C00H
ADD HL,BC
LD B,H
LD C,L
LD HL,BUFF
LD E,40
LD D,E
GL1:CALL TOBC
IN A,(VDATA)
LD (HL),A
INC HL
INC BC
DEC E
JR NZ,GL1
GL2:
DEC D
JR Z,GL4
DEC HL
LD A,(HL)
CP 32
JR Z,GL2
GL4:INC HL
LD (HL),13
POP BC
POP AF
RET

INCL ASSY

END:END