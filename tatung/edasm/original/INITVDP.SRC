


ORG 8000H
LOAD 8000H


;PORT ADDRESSES SHOULD BE READ FROM:-

;VDPDATA FROM LOCATION 6 IN ROM
;VDPADD ------"------- 7 --"---

;HAVE ALWAYS BEEN VALUES BELOW ON ALL
;MSX'S PRODUCED SO FAR
;BUT MIGHT BE DIFFERENT ON MSX2


VDPADD:EQU 99H
VDPDATA:EQU 98H


CALL INITREGS
CALL CLS
RET


WR_BLOCK:

;WRITES BLOCK FROM ADD IN HL
;TO VIDIO RAM ADD IN BC
;LENGTH DE (0 < DE < 3FFFH)

DEC BC
LD A,B
AND 3FH
LD B,A
CALL TOBC
LD C,VDPDATA
LD B,E
INC B
DEC B
JR Z,DUNBLOCK
NXTBLK:OUTI ;             16
NOP ;              4
JR NZ,NXTBLK    ;12
;                           =32
;MUST BE MINIMUM OF 32 CC
;BETWEEN OUT'S ON WRITE BLOCK
;OR IN'S ON READ BLOCK

DUNBLOCK:DEC D
JP P,NXTBLK
RET

RD_BLOCK:

;READS BLOCK INTO ADD IN HL
;FROM VIDIO RAM ADD IN BC
;LENGTH DE (0 < DE < 3FFFH)

CALL TOBC
LD C,VDPDATA
LD B,E
INC B
DEC B
JR Z,DUNRB
NXTRD:INI
NOP
JR NZ,NXTRD
DUNRB:DEC D
JP P,NXTRD
RET


FILLBLOCK:
LD (TEMP),A
DEC BC
LD A,B
AND 3FH
LD B,A
CALL TOBC
LD HL,TEMP
LD C,VDPDATA
INC B
DEC B
JR Z,DUNFB
NXTFBLK:OUTI 
DEC HL
JR NZ,NXTFBLK
DUNFB:DEC D
JP P,NXTFBLK
RET

TEMP:DB 0


CLS:LD BC,0     ;CLR PATTERNS
LD E,0
LD DE,1800H
CALL FILLBLOCK
LD BC,2000H ;CLR COLOUR
LD E,07
LD DE,1800H
CALL FILLBLOCK

LD BC,17FFH ; NAME TABLE-1
CALL TOBC
LD B,0      ; FILL WITH
LD E,3      ; 0 TO 256
INITNAMT:LD A,B      ; THREE TIMES
OUT (VDPDATA),A
INC B
PUSH AF       ;SMALL DELAY
POP AF
JR NZ,INITNAMT
DEC E
JR NZ,INITNAMT

RET

INITREGS:
LD HL,VDPMSX
LD A,8
LD B,80H
INIT1R:LD C,(HL)
CALL TOBC
INC HL
INC B
DEC A
JR NZ,INIT1R
RET

;TOBC IF BITS 6 & 7 OF REG B BOTH 0
;     WILL WRITE AN ADDRESS TO VDP
;ELSE IF BIT 7 SET
;     WITES VALUE (C) TO REG (B)

TOBC:
PUSH AF
LD A,C
OUT (VDPADD),A
PUSH AF   ;DELAY
POP AF
LD A,B
OUT (VDPADD),A
POP AF
RET


VDPMSX:DB 02H,0E0H,06H,0FFH
DB 03H,36H,07H,04H

;ABOVE VALUES ARE FOR HIRES MODE

;VIDIO RAM MAP
;NAME TABLE           1800 TO 1AFF
;PATTERN TABLE           0 TO 17FF
;COLOUR TABLE         2000 TO 27FF
;(LOW NIBLE IS BACKGROUND)
;SPRITE ATTR          1B00 TO ????
;SPRITE PATTERNS      3800 TO ????


;KEYBOARD LAYOUT AS FOLLOWS
;FROM TOSHIBA MACHINE

;      BIT READ FROM PORT A9H
;     7   6   5   4   3   2   1   0


;90   7   6   5   4   3   2   1   0
;91   :   )   (   |   +   -   9   8
;92   B   A   `   ?   >   <  XXX  "
;93   J   I   H   G   F   E   D   C
;94   R   Q   P   O   N   M   L   K
;95   Z   Y   X   W   V   U   T   S
;96   F3  F2  F1 COD CAP GRF CTR  SHIFT
;97  RET SEL  BS STP TAB ESC F5   F4
;98  RGC DWC UPC LCR DEL INS HOM  SP


;^ VALUE TO WRITE TO PORT AA

;XXX = FUNNY SQUIGLE NEXT TO RETURN